#ifndef ORO_CORBA_SERVICE_PROVIDER_IDL
#define ORO_CORBA_SERVICE_PROVIDER_IDL

#ifdef CORBA_IS_TAO
//resolves LNK2005
#include <tao/orb.idl>
#endif

#include "ConfigurationInterface.idl"
#include "OperationInterface.idl"
#include "DataFlow.idl"

module RTT
{
    module corba
    {

    interface CService;
    struct CServiceDescription;
    typedef sequence<CService> CServices;
    typedef sequence<CServiceDescription> CServiceDescriptions;

    /**
     * Complete description of a Service
     * @ingroup CompIDL
     */
    struct CServiceDescription
    {
        string name;
        string description;
        COperationInterface::COperationDescriptions operations;
        CConfigurationInterface::CPropertyNames properties;
        CConfigurationInterface::CAttributeNames attributes;
        CDataFlowInterface::CPortDescriptions ports;

        CServices children;
        CServiceDescriptions children_descriptions;
    };

	/**
	 * An Orocos Service which hosts operations, attributes and properties.
	 * @ingroup CompIDL
	 */
	interface CService : COperationInterface, CConfigurationInterface, CDataFlowInterface
	{
	    typedef sequence<string> CProviderNames;

	    /**
	     * Return the name of this service.
	     */
	    string getName();

	    /**
	     * Return the description of this service.
	     */
        string getServiceDescription();

        /**
         * Return the full description of this service
         * that includes all Service objects
         */
        CServiceDescription getCServiceDescription();

	    /**
	     * Get a list of all the child services this service offers.
	     */
	    CProviderNames getProviderNames( );

	    /**
	     * Get a child service this service provides.
	     */
	    CService getService( in string name );

	    /**
	     * Has this service a child service with given name ?
	     */
	    boolean hasService( in string name );

	};

    };
};
#endif
